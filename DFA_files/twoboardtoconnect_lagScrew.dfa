#! NX/KF 4.0
DefClass: twoboardtoconnect_lagScrew (ug_base_part);
#CSYS
(vector parameter) direction: vector(0,0,1);
(point parameter) start_point: point(15,0,0);
#the dimensions of the 2 boards
    (number parameter) L_1: 10;
    (number parameter) W_1: 5;
    (number parameter) Th_1: 2.5;
    (number parameter) L_2: 5;
    (number parameter) W_2: 10;
    (number parameter) Th_2: 0.2;
#the position to connect
    (point parameter) connect_point: point(min(L_1:, L_2:)/2,min(W_1:, W_2:)/2,Th_1: + Th_2:) + (start_point: - point(0,0,0));
#the required mechanical performance 
    (number parameter) connection_capacity: 12;
    (number parameter) connection_requirement: 10;
    
    (number parameter) diameter_cylinder: 0.375;
    (number parameter) length_lagscrew: 2.5;
    
    (number parameter) outter_dia_washer: 2*diameter_cylinder:;
    (number parameter) inner_dia_washer: diameter_cylinder:;
    (number parameter) thickness_washer: 0.125;

(Child) board1: {
    class, ug_block;
    length, L_1:;
    width, W_1:;
    height, Th_1:;
    origin, start_point:;
};

(Child) board2: {
    class, ug_block;
    length, L_2:;
    width, w_2:;
    height, Th_2:;
    origin, point(0, 0, Th_1:) + (start_point: - point(0,0,0));
};

(Child) hole1: {
    Class,  ug_cylinder;
    Origin, connect_point:;
    Diameter, diameter_cylinder:;
    Direction, -direction:;
    Height, length_lagscrew:;
    Operation, Subtract;
    target, {board1:};    
};

(Child) hole2: {
    Class,  ug_cylinder;
    Origin, connect_point:;
    Diameter, diameter_cylinder:;
    Direction, -direction:;
    Height, Th_2:;
    Operation, Subtract;
    target, {board2:};
};

(Child) lag_screw: {
    Class, lag_screw_zzz;
    start_point, connect_point:-vector(0,0, length_lagscrew: - thickness_washer:);
    diameter, diameter_cylinder:;
    length_total, length_lagscrew:;
};

(Child) washer: {
    Class,  ug_cylinder;
    Origin, connect_point:;
    Diameter, outter_dia_washer:;
    Height, thickness_washer:;
    Direction, direction:;
};

(Child) hole3: {
    Class,  ug_cylinder;
    Origin, connect_point:;
    Diameter, inner_dia_washer:;
    Height, thickness_washer:;
    Direction, direction:;
    Operation, Subtract;
    target, {washer:};
};

(Child) body_colored: {
    Class, ug_body; 
    Feature, {board2:};
    color,  if(connection_capacity: < connection_requirement:) then ug_askClosestColor(RED) 
       else if(connection_capacity: > connection_requirement:) then ug_askClosestColor(GREEN)
       else ug_askClosestColor(YELLOW); 
};
